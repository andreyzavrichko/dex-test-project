plugins {
    id 'java'
    id "io.qameta.allure" version "2.9.6"
}

repositories {
    mavenCentral()
}

allure {
    report {
        version.set("2.17.2")
    }
    adapter {
        autoconfigure.set(true)
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.17.2")
            }
        }
    }
}

def selenideVersion = "5.24.0",
    junitVersion = "5.8.2",
    fakerVersion = "1.0.2",
    slf4jVersion = "1.7.32",
    allureVersion = "2.17.2",
    pdfVersion = "1.7.0",
    xlsVersion = "1.4.0",
    assertjVersion = "3.22.0",
    csvVersion = "5.5.2"

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "com.github.javafaker:javafaker:$fakerVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "com.codeborne:pdf-test:$pdfVersion",
            "com.codeborne:xls-test:$xlsVersion",
            "com.opencsv:opencsv:$csvVersion",
            "commons-io:commons-io:2.11.0"
    )

    testRuntimeOnly(
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
            "com.browserup:browserup-proxy-core:2.0.1")

}

compileTestJava {
    options.encoding = 'UTF-8'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
//            events "started", "failed"
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}